      SUBROUTINE FUND_ARG40 (jd,ct,cent,f,fd)
C
C  SUBROUTINE FUND_ARG computes the fundamental
C  arguments and the number of Julian centuries since J2000
C  to be used in the nutation series and UT1R calculation.
C
C  Linear coefficient for longitude of the asc.node of the moon's
C  mean orbit on the ecliptic Omega was written as in IERS2000.
C
C    Input variables:
C           1. jd   -  The Julian date at zero hours UTC OF THE DATE  (DAYS)
C           2. ct   -  The coordinate time fraction of the coordinate time day.
C                      (DAYS)
C    Output variables:
C           1. cent -  The number of Julian centuries elapsed since the epoch
C                      January 1.5 2000.(Centuries)
C           2. f(5) -  The fundamental arguments for the nutation theory.
C                      (IERS Conventions 1996) (Arcseconds),
C           namely,
C              F(1) = mean anomaly of the moon
C                   = mean longitude of the moon (L) minus the
C                     mean longitude of the moon's perigee       =  (l)
C              F(2) = mean anomaly of the sun
C                   = mean longitude of the sun (L') minus the
C                     mean longitude of the sun's perigee        =  (l')
C              F(3) = mean longitude of the moon (L) minus omega =  (F)
C              F(4) = mean elongation of the moon from the sun   =  (D)
C              F(5) = longitude of the asc.node of the moon's
C                     mean orbit on the ecliptic,
C                     measured from the mean equinox of date     =  (omega)
C           3. fd(5)- The CT time derivatives of the fundamental arguments.
C                      (arcsec/century)
C
C----------------------------------------------------------------------
C   SUBROUTINE FUND_ARG was written by V.Zharov 
C
C   VERSION 1.0   21 May 2005
C
C   VERSION 4.0   21 July 2012
C   Change for l': 
C   -0.000136d0 on +0.000136d0
C----------------------------------------------------------------------
      IMPLICIT NONE
C
      Integer KNUTD
      Real(8) jd, ct, cent, f(5), fd(5)
      Real(8) f1(5), f2(5), f3(5), f4(5), f5(5), t, t2, t3, t4, days
      Real(8) g1, g2, g3, g4, g5, sec360
	Real(8) CDEGRAD, CARCRAD, CTIMRAD, SECDAY, JD2000, JUL_CENT
C
	COMMON /MATH2/ CDEGRAD, CARCRAD, CTIMRAD, SECDAY, JD2000, JUL_CENT

C     Constants used -
C       f1(5)   - coefficients used in the calculation of l
C       f2(5)   - -----------//-------------//----------  l'
C       f3(5)   - -----------//-------------//----------  F
C       f4(5)   - -----------//-------------//----------  D
C       f5(5)   - -----------//-------------//----------  Omega
C
C     taken from IERS Conventions (2003)-
      data f1     / -0.00024470d0,       0.051635d0,  31.8792d0,
     *          1717915923.2178d0,   485868.249036d0/
      data f2     / -0.00001149d0,      +0.000136d0,  -0.5532d0,
     *           129596581.0481d0,  1287104.793048d0/
      data f3     /  0.00000417d0,      -0.001037d0, -12.7512d0,
     *          1739527262.8478d0,   335779.526232d0/
      data f4     / -0.00003169d0,       0.006593d0,  -6.3706d0,
     *          1602961601.2090d0,  1072260.703692d0/
      data f5      /-0.00005939d0,       0.007702d0,   7.4722d0,
     *            -6962890.5431d0,   450160.398036d0/
C
      data sec360 / 1296000.0D0 /       ! arcseconds in one turn
C
C  Compute the number of Julian days elapsed since the epoch January 1.5, 2000.
      days = (jd - jd2000) + ct
C
C  Compute the number of Julian centuries elapsed since the epoch January 1.5, 2000.
      t  = days / jul_cent
      t2 = t * t
      t3 = t * t2
      t4 = t * t3
	cent = t
C
C  Computation of the fundamental arguments and derivatives
C
      g1 = f1(1)*t4 + f1(2)*t3 + f1(3)*t2 + f1(4)*t + f1(5)
      f(1) = DMOD( g1, sec360 )
      fd(1) = 4.D0*f1(1)*t3 + 3.D0*f1(2)*t2 + 2.D0*f1(3)*t + f1(4)
C
      g2 = f2(1)*t4 + f2(2)*t3 + f2(3)*t2 + f2(4)*t + f2(5)
      f(2) = DMOD( g2, sec360 )
      fd(2) = 4.D0*f2(1)*t3 + 3.D0*f2(2)*t2 + 2.D0*f2(3)*t + f2(4)
C
      g3 = f3(1)*t4 + f3(2)*t3 + f3(3)*t2 + f3(4)*t + f3(5)
      f(3) = DMOD( g3, sec360 )
      fd(3) = 4.D0*f3(1)*t3 + 3.D0*f3(2)*t2 + 2.D0*f3(3)*t + f3(4)
C
      g4 = f4(1)*t4 + f4(2)*t3 + f4(3)*t2 + f4(4)*t + f4(5)
      f(4) = DMOD( g4, sec360 )
      fd(4) = 4.D0*f4(1)*t3 + 3.D0*f4(2)*t2 + 2.D0*f4(3)*t + f4(4)
C
      g5 = f5(1)*t4 + f5(2)*t3 + f5(3)*t2 + f5(4)*t + f5(5)
      f(5) = DMOD( g5, sec360 )
      fd(5) = 4.D0*f5(1)*t3 + 3.D0*f5(2)*t2 + 2.D0*f5(3)*t + f5(4)
C-------------------------------------------------------------------------------
C  KNUTD for debug output (/= 0)
       KNUTD = 0
C-------------------------------------------------------------------------------
C  Debug output
      IF (KNUTD.NE.0) THEN
	  WRITE(*,  *) 'Debug output for subroutine FUND_ARG'
	  write(*, 20) '    JD   ', JD
	  write(*, 20) '    CT   ', CT
	  write(*, 20) '  DAYS   ', days
	  write(*, 20) '     t   ', t
C
        write(*, 20) ' g1      ',g1
        write(*, 20) ' f(1)    ',f(1)
        write(*, 20) ' df(1)/dt',fd(1)
        write(*, 20) ' g2      ',g2
        write(*, 20) ' f(2)    ',f(2)
        write(*, 20) ' df(2)/dt',fd(2)
        write(*, 20) ' g3      ',g3
        write(*, 20) ' f(3)    ',f(3)
        write(*, 20) ' df(3)/dt',fd(3)
        write(*, 20) ' g4      ',g4
        write(*, 20) ' f(4)    ',f(4)
        write(*, 20) ' df(4)/dt',fd(4)
        write(*, 20) ' g5      ',g5
        write(*, 20) ' f(5)    ',f(5)
        write(*, 20) ' df(5)/dt',fd(5)
 20     FORMAT (a,  D25.16 )
       pause
      ENDIF
C
      END

